// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-beta04'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Config.kotlinVersion}"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.4.2.1"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.21.0"
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                buildToolsVersion Config.buildTools
                compileSdkVersion Config.compileSdk

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                sourceSets.each { set ->
                    set.java.srcDirs += "src/${set.name}/kotlin"
                }

                buildTypes {
                    debug {
                        aaptOptions.cruncherEnabled = false
                        splits.abi.enable = false
                        splits.density.enable = false
                    }
                }

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
